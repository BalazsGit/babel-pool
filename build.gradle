buildscript {
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath 'nu.studer:gradle-jooq-plugin:3.0.2'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
}

plugins {
    id "org.flywaydb.flyway" version "5.2.4"
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'nu.studer.jooq'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def dburl =  "jdbc:h2:./build/burstPoolDb"
def dbusername = "sa"

dependencies {
    implementation 'org.apache.poi:poi-ooxml:3.17'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.15'
    implementation 'com.github.burst-apps-team:burstkit4j:v0.9-beta5'
    implementation 'org.nanohttpd:nanohttpd:2.2.0'
    implementation "org.flywaydb:flyway-core:5.2.4"
    implementation "org.jooq:jooq"
    implementation "org.jooq:jooq-meta"
    implementation "org.jooq:jooq-codegen"
    implementation "org.flywaydb:flyway-core:2.5.4"
    //jooqRuntime 'org.hsqldb:hsqldb:2.4.1'
    compile "com.h2database:h2:1.4.197"
    jooqRuntime "com.h2database:h2:1.4.197"
}

flyway {
    url = "${dburl}"
    user = "${dbusername}"
    schemas = ["public"]
    locations = ["filesystem:src/main/resources/db/migration"]
}

jooq {
    version = "3.11.9"
    edition = "OSS"
    burstPool(sourceSets.main) {
        jdbc {
            url = "${dburl}"
            user = "${dbusername}"
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                includes = ".*"
                inputSchema = "public"
            }
            target {
                packageName = "burst.pool.db"
                directory = "build/generated/source/jooq/main/java"
            }
        }
    }
}

generateBurstPoolJooqSchemaSource.dependsOn flywayMigrate

task buildJar(dependsOn: shadowJar)

jar {
    manifest {
        attributes 'Main-Class': 'burst.pool.Launcher'
    }
}

sourceSets {
    main {
        java {
            srcDirs "build/generated/source/jooq/main/java"
        }
    }
}
