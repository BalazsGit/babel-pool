buildscript {
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath 'nu.studer:gradle-jooq-plugin:3.0.3'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
}

plugins {
    id "org.flywaydb.flyway" version "6.0.0"
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'nu.studer.jooq'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def dburl =  "jdbc:mariadb://localhost:3306/pooldb"
def dbusername = "root"

dependencies {
    implementation 'org.apache.poi:poi-ooxml:4.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.11'
    implementation 'com.github.burst-apps-team:burstkit4j:0.12.6'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation "org.flywaydb:flyway-core:5.2.4"
    implementation "org.jooq:jooq"
    implementation "org.jooq:jooq-meta"
    implementation "org.jooq:jooq-codegen"
    implementation "org.apache.logging.log4j:log4j-api:2.12.1"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.12.1"
    implementation "org.slf4j:slf4j-api:1.7.28"
    implementation 'com.zaxxer:HikariCP:3.3.1'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation group: 'org.ehcache', name: 'ehcache', version: '3.8.0'

    // Needed for build script so "compile" needed
    //compile "com.h2database:h2:1.4.197"
    //jooqRuntime "com.h2database:h2:1.4.197"
    compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.4.3'
    jooqRuntime group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.4.1'
}

flyway {
    url = "${dburl}"
    user = "${dbusername}"
    locations = ["filesystem:"+project.projectDir.toString()+"/src/main/resources/db/migration"]
}

jooq {
    version = "3.11.9"
    edition = "OSS"
    burstPool(sourceSets.main) {
        jdbc {
            url = "${dburl}"
            user = "${dbusername}"
        }
        generator {
            name = 'org.jooq.codegen.JavaGenerator'
            database {
                includes = ".*"
                name = "org.jooq.meta.mariadb.MariaDBDatabase"
                inputSchema = "pooldb"
                outputSchemaToDefault = true
            }
            target {
                packageName = "burst.pool.db"
                directory = "src/main/java"
            }
        }
    }
}

generateBurstPoolJooqSchemaSource.dependsOn flywayMigrate
generateBurstPoolJooqSchemaSource.onlyIf {System.getProperty("generateSchema") == "true"}
flywayMigrate.onlyIf {System.getProperty("generateSchema") == "true"}

task removeJsBundle << {
    delete {
        delete "$projectDir/client/build/bundle/main.bundle.js"
        delete "$projectDir/src/main/resources/html/js/"
    }
}

task copyJsBundle << {
    copy {
        from file("$projectDir/client/build/bundle/main.bundle.js")
        into file("$projectDir/src/main/resources/html/js/")
    }
}

copyJsBundle.dependsOn(removeJsBundle, ':client:bundle')
assemble.dependsOn copyJsBundle

jar {
    manifest {
        attributes 'Main-Class': 'burst.pool.Launcher'
    }
}
